################################################################################
#
# RevBayes Validation Test: Bernoulli Distribution
#
# Model: 20 random variables from a Bernoulli distribution.
#
#
# authors: Sebastian Hoehna
#
################################################################################

## Global settings

NUM_VALIDATION_REPLICATES = 1000
NUM_MCMC_ITERATIONS       = 10000


# create the monitor and moves vectors
moves     = VectorMoves()
monitors  = VectorMonitors()

n_samples = 20


alpha <- 1
beta <- 1

p ~ dnBeta(alpha,beta)
moves.append( mvBetaProbability(p,weight=2) )

for (i in 1:n_samples) {
    x[i] ~ dnBernoulli(p)
    x[i].clamp(0)
}


#############
# THE Model #
#############

# We define our model.
# We can use any node of our model as a handle, here we chose to use the rate matrix.
mymodel = model(p)




mymcmc = mcmc(mymodel, monitors, moves)

validation = validationAnalysis( mymcmc, NUM_VALIDATION_REPLICATES, directory="output_Bernoulli" )
validation.burnin(generations=0.1*NUM_MCMC_ITERATIONS,tuningInterval=100)
validation.run(generations=NUM_MCMC_ITERATIONS)
validation.summarize(coverageProbability=0.9)
validation.summarize(coverageProbability=0.5)


# you may want to quit RevBayes now
q()
