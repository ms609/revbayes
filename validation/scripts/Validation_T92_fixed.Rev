################################################################################
#
# RevBayes Validation Test: Tamura (92) with fixed time tree
#
# Model: Sequences evolve under the Tamura (92) model and the tree is fixed
#
#
# authors: Sebastian Hoehna
#
################################################################################

## Global settings

NUM_VALIDATION_REPLICATES = 1000
NUM_MCMC_ITERATIONS       = 10000


# create the monitor and moves vectors
moves     = VectorMoves()
monitors  = VectorMonitors()


#######################
# Reading in the Data #
#######################

data <- readDiscreteCharacterData("data/primates_small.nex")



######################
# Substitution Model #
######################

#### specify the HKY substitution model applied uniformly to all sites ###


gc ~ dnBeta(1,1)
moves.append( mvSlide(gc,weight=2) )

tstv ~ dnGamma(2,2)
moves.append( mvSlide(tstv,weight=2) )
moves.append( mvScale(tstv,weight=2) )

Q := fnT92(kappa=tstv, gc=gc)



##############
# Tree model #
##############

psi <- readTrees( "data/primates_small.tre" )[1]




###################
# PhyloCTMC Model #
###################

# We use an empirical estimate of the clock rate which is 0.01 (=1%) per million years per site
clockRate <- 0.005

# the sequence evolution model
seq ~ dnPhyloCTMC(tree=psi, Q=Q, branchRates=clockRate, type="DNA")

# attach the data
seq.clamp(data)




#############
# THE Model #
#############

# We define our model.
# We can use any node of our model as a handle, here we chose to use the rate matrix.
mymodel = model(Q)




mymcmc = mcmc(mymodel, monitors, moves)


validation = validationAnalysis( mymcmc, NUM_VALIDATION_REPLICATES, directory="output_T92_fixed" )
validation.burnin(generations=0.1*NUM_MCMC_ITERATIONS,tuningInterval=100)
validation.run(generations=NUM_MCMC_ITERATIONS)
validation.summarize(coverageProbability=0.9)
validation.summarize(coverageProbability=0.5)


# you may want to quit RevBayes now
q()
